# Copyright (c) 2024 The Brave Authors. All rights reserved.
# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this file,
# You can obtain one at https://mozilla.org/MPL/2.0/.

import("//unknown/components/tor/buildflags/buildflags.gni")
import("//build/config/features.gni")

assert(toolkit_views)

source_set("location_bar") {
  sources = [
    "brave_location_bar_view.cc",
    "brave_location_bar_view.h",
    "brave_search_conversion/promotion_button_controller.cc",
    "brave_search_conversion/promotion_button_controller.h",
    "brave_search_conversion/promotion_button_view.cc",
    "brave_search_conversion/promotion_button_view.h",
    "brave_selected_keyword_view_util.cc",
    "brave_selected_keyword_view_util.h",
    "brave_shields_page_info_controller.cc",
    "brave_shields_page_info_controller.h",
    "brave_star_view.cc",
    "brave_star_view.h",
  ]

  deps = [
    "//unknown/app:brave_generated_resources_grit",
    "//unknown/app/theme:brave_theme_resources",
    "//unknown/app/vector_icons",
    "//unknown/browser/ui:brave_tab_prefs",
    "//unknown/browser/ui/page_info",
    "//unknown/browser/ui/views/page_info",
    "//unknown/components/brave_search_conversion",
    "//unknown/components/commander/browser",
    "//unknown/components/commander/common",
    "//unknown/components/resources:strings",
    "//unknown/components/tor/buildflags",
    "//unknown/components/vector_icons",
    "//chrome/browser/image_fetcher",
    "//chrome/browser/profiles",
    "//chrome/browser/search_engines",
    "//chrome/browser/themes",
    "//chrome/browser/ui:layout_constants",
    "//chrome/browser/ui/color:mixers",
    "//chrome/browser/ui/omnibox",
    "//chrome/browser/ui/tabs:tab_strip",
    "//chrome/browser/ui/views/location_bar",
    "//chrome/browser/ui/views/page_action",
    "//chrome/browser/ui/views/page_info",
    "//components/image_fetcher/core",
    "//components/search_engines",
    "//components/version_info:channel",
    "//ui/gfx",
    "//ui/views",
  ]

  if (enable_tor) {
    sources += [
      "onion_location_view.cc",
      "onion_location_view.h",
    ]

    deps += [
      "//unknown/browser/tor",
      "//unknown/components/tor",
    ]
  }
}

source_set("browser_tests") {
  testonly = true
  defines = [ "HAS_OUT_OF_PROC_TEST_RUNNER" ]

  sources = [ "//unknown/browser/ui/views/location_bar/brave_location_bar_view_browsertest.cc" ]

  deps = [
    ":location_bar",
    "//base",
    "//unknown/components/brave_search_conversion",
    "//chrome/browser",
    "//chrome/browser/search_engines",
    "//chrome/browser/ui",
    "//chrome/common",
    "//chrome/test",
    "//chrome/test:test_support",
    "//components/prefs",
    "//ui/base:test_support",
  ]
}
