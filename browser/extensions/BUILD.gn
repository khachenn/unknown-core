# Copyright (c) 2022 The Brave Authors. All rights reserved.
# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this file,
# You can obtain one at https://mozilla.org/MPL/2.0/.

import("//unknown/components/ai_chat/core/common/buildflags/buildflags.gni")
import("//unknown/components/brave_vpn/common/buildflags/buildflags.gni")
import("//unknown/components/brave_wallet/common/buildflags/buildflags.gni")
import("//unknown/components/brave_wayback_machine/buildflags/buildflags.gni")
import("//unknown/components/speedreader/common/buildflags/buildflags.gni")
import("//unknown/components/tor/buildflags/buildflags.gni")
import("//unknown/components/web_discovery/buildflags/buildflags.gni")
import("//build/config/features.gni")
import("//build/config/ui.gni")
import("//components/gcm_driver/config.gni")

assert(toolkit_views)

source_set("component_loader") {
  sources = [
    "brave_component_loader.cc",
    "brave_component_loader.h",
  ]

  deps = [
    "//base",
    "//unknown/components/brave_extension:static_resources",
    "//chrome/browser/extensions",
    "//chrome/browser/profiles:profile",
    "//components/prefs",
    "//extensions/browser",
    "//extensions/common",
    "//ui/base",
  ]

  if (enable_web_discovery_native) {
    deps += [ "//unknown/components/web_discovery/common" ]
  }
}

# This is split out from extensions and the rest of brave_prefs_util because it
# has circular dependencies on //chrome/browser.
# https://github.com/brave/brave-browser/issues/41418
source_set("brave_prefs_util_impl") {
  visibility = [
    ":*",
    "//chrome/browser",
  ]
  sources = [ "api/settings_private/brave_prefs_util_impl.cc" ]

  deps = [
    "//unknown/components/brave_shields/core/browser",
    "//chrome/browser:browser_public_dependencies",
    "//chrome/browser/content_settings:content_settings_factory",
    "//chrome/browser/profiles:profile",
    "//chrome/common/extensions/api",
    "//components/content_settings/core/browser:cookie_settings",
    "//components/content_settings/core/common",
    "//content/public/browser",
    "//url",
  ]
}

source_set("extensions") {
  sources = [
    "api/brave_extensions_api_client.cc",
    "api/brave_extensions_api_client.h",
    "api/brave_rewards_api.cc",
    "api/brave_rewards_api.h",
    "api/brave_talk_api.cc",
    "api/brave_talk_api.h",
    "api/brave_theme_api.cc",
    "api/brave_theme_api.h",
    "api/identity/brave_web_auth_flow.cc",
    "api/identity/brave_web_auth_flow.h",
    "api/settings_private/brave_prefs_util.cc",
    "api/settings_private/brave_prefs_util.h",
    "api/web_discovery_api.cc",
    "api/web_discovery_api.h",
    "brave_extension_management.cc",
    "brave_extension_management.h",
    "brave_extension_provider.cc",
    "brave_extension_provider.h",
    "brave_extensions_browser_api_provider.cc",
    "brave_extensions_browser_api_provider.h",
    "brave_extensions_browser_client_impl.cc",
    "brave_extensions_browser_client_impl.h",
    "updater/brave_update_client_config.cc",
    "updater/brave_update_client_config.h",
  ]

  deps = [
    ":brave_prefs_util_impl",
    ":resources",
    "//base",
    "//unknown/app:brave_generated_resources_grit",
    "//unknown/browser:browser_process",
    "//unknown/browser/brave_adaptive_captcha",
    "//unknown/browser/brave_ads",
    "//unknown/browser/brave_rewards",
    "//unknown/browser/brave_rewards:util",
    "//unknown/browser/brave_search",
    "//unknown/browser/component_updater",
    "//unknown/browser/extensions/api:generated_api_registration",
    "//unknown/browser/ui:brave_tab_prefs",
    "//unknown/common",
    "//unknown/common/extensions/api",
    "//unknown/components/ai_chat/core/common/buildflags",
    "//unknown/components/brave_adaptive_captcha",
    "//unknown/components/brave_ads/browser",
    "//unknown/components/brave_ads/core/public:headers",
    "//unknown/components/brave_extension:static_resources",
    "//unknown/components/brave_news/common",
    "//unknown/components/brave_rewards/content",
    "//unknown/components/brave_rewards/core",
    "//unknown/components/brave_rewards/core/mojom",
    "//unknown/components/brave_search/browser",
    "//unknown/components/brave_shields/core/common",
    "//unknown/components/brave_vpn/common/buildflags",
    "//unknown/components/brave_wallet/common/buildflags",
    "//unknown/components/brave_wayback_machine/buildflags",
    "//unknown/components/constants",
    "//unknown/components/de_amp/common",
    "//unknown/components/debounce/core/common",
    "//unknown/components/ntp_background_images/common",
    "//unknown/components/playlist/core/common",
    "//unknown/components/request_otr/common",
    "//unknown/components/sidebar/browser",
    "//unknown/components/speedreader/common/buildflags",
    "//unknown/components/tor/buildflags",
    "//unknown/components/web_discovery/buildflags",
    "//unknown/components/webcompat_reporter/common",
    "//chrome/browser:browser_process",
    "//chrome/browser:browser_public_dependencies",
    "//chrome/browser/extensions",
    "//chrome/browser/extensions:chrome_extensions_browser_client",
    "//chrome/browser/profiles:profile",
    "//chrome/browser/ui",
    "//chrome/browser/ui/browser_window",
    "//chrome/common",
    "//components/browsing_data/core",
    "//components/gcm_driver:gcm_buildflags",
    "//components/prefs",
    "//components/update_client:network_impl",
    "//components/user_prefs",
    "//content/public/browser",
  ]

  public_deps = [
    ":component_loader",
    "//unknown/components/brave_ads/core/mojom",
    "//unknown/components/brave_rewards/core/mojom",
    "//extensions/browser",
  ]

  if (use_gcm_from_platform) {
    deps += [ "//components/gcm_driver" ]
  }

  if (enable_brave_wayback_machine) {
    deps += [ "//unknown/components/brave_wayback_machine" ]
  }

  if (enable_speedreader) {
    deps += [ "//unknown/components/speedreader" ]
  }

  if (enable_ai_chat) {
    deps += [ "//unknown/components/ai_chat/core/common" ]
  }

  if (enable_brave_vpn) {
    deps += [ "//unknown/components/brave_vpn/common" ]
  }

  if (enable_tor) {
    deps += [
      "//unknown/browser/tor",
      "//unknown/components/tor",
      "//unknown/components/tor:pref_names",
    ]
  }

  if (enable_web_discovery_native) {
    deps += [ "//unknown/components/web_discovery/common" ]
  }

  if (is_win) {
    deps += [ "//unknown/components/windows_recall" ]
  }

  if (enable_brave_wallet) {
    deps += [
      "//unknown/components/brave_wallet/browser",
      "//unknown/components/brave_wallet/browser:pref_names",
      "//unknown/components/brave_wallet/browser:utils",
      "//unknown/components/brave_wallet/common",
      "//unknown/components/decentralized_dns/core",
    ]
  }

  allow_circular_includes_from = [ ":brave_prefs_util_impl" ]
}

group("resources") {
  deps = [
    "//unknown/components/brave_extension:generated_resources",
    "//unknown/components/brave_extension:static_resources",
  ]
}

source_set("test_support") {
  testonly = true

  sources = [
    "brave_base_local_data_files_browsertest.cc",
    "brave_base_local_data_files_browsertest.h",
    "brave_extension_functional_test.cc",
    "brave_extension_functional_test.h",
  ]

  deps = [
    "//base",
    "//base/test:test_support",
    "//unknown/browser",
    "//unknown/components/brave_component_updater/browser",
    "//unknown/components/constants",
    "//chrome/browser/extensions",
    "//chrome/test:test_support",
    "//content/public/browser",
    "//content/test:test_support",
    "//net",
    "//net:test_support",
  ]
}
