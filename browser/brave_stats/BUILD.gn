# Copyright (c) 2022 The Brave Authors. All rights reserved.
# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this file,
# You can obtain one at http://mozilla.org/MPL/2.0/. */

import("//unknown/browser/brave_stats/buildflags.gni")
import("//unknown/components/brave_wallet/common/buildflags/buildflags.gni")
import("//build/buildflag_header.gni")

buildflag_header("buildflags") {
  header = "buildflags.h"
  flags = [ "BRAVE_USAGE_SERVER=\"$brave_stats_updater_url\"" ]
}

source_set("brave_stats") {
  sources = [
    "//unknown/browser/brave_stats/brave_stats_updater.cc",
    "//unknown/browser/brave_stats/brave_stats_updater.h",
    "//unknown/browser/brave_stats/brave_stats_updater_params.cc",
    "//unknown/browser/brave_stats/brave_stats_updater_params.h",
    "//unknown/browser/brave_stats/features.cc",
    "//unknown/browser/brave_stats/features.h",
    "//unknown/browser/brave_stats/first_run_util.cc",
    "//unknown/browser/brave_stats/first_run_util.h",
    "//unknown/browser/brave_stats/switches.h",
  ]

  deps = [
    "//base",
    "//unknown/browser:browser_process",
    "//unknown/browser/brave_stats:buildflags",
    "//unknown/common",
    "//unknown/components/brave_ads/core",
    "//unknown/components/brave_referrals/common",
    "//unknown/components/brave_stats/browser",
    "//unknown/components/brave_wallet/common/buildflags",
    "//unknown/components/constants",
    "//unknown/components/misc_metrics",
    "//unknown/components/rpill/common",
    "//unknown/components/version_info",
    "//chrome/browser:browser_process",
    "//chrome/browser/headless",
    "//chrome/browser/profiles:profile",
    "//chrome/browser/ui",
    "//chrome/common",
    "//components/prefs",
    "//net",
    "//services/network/public/cpp",
  ]

  if (enable_brave_wallet) {
    deps += [
      "//unknown/components/brave_wallet/browser",
      "//unknown/components/brave_wallet/browser:pref_names",
    ]
  }
}

source_set("browser_tests") {
  testonly = true

  sources = [ "//unknown/browser/brave_stats/brave_stats_updater_browsertest.cc" ]

  deps = [
    "//unknown/browser/brave_stats",
    "//unknown/components/brave_referrals/browser",
    "//chrome/browser",
    "//chrome/test:test_support",
  ]

  defines = [ "HAS_OUT_OF_PROC_TEST_RUNNER" ]
}

source_set("unit_tests") {
  testonly = true

  sources = [
    "//unknown/browser/brave_stats/brave_stats_updater_unittest.cc",
    "//unknown/browser/brave_stats/first_run_util_unittest.cc",
  ]

  deps = [
    "//unknown/browser/brave_ads",
    "//unknown/browser/brave_stats",
    "//unknown/components/brave_ads/core",
    "//unknown/components/brave_referrals/browser",
    "//unknown/components/brave_rewards/content",
    "//unknown/components/brave_stats/browser",
    "//unknown/components/brave_wallet/common/buildflags",
    "//unknown/components/misc_metrics",
    "//chrome/browser",
    "//chrome/browser/prefs",
    "//chrome/test:test_support",
    "//components/prefs",
  ]

  if (enable_brave_wallet) {
    deps += [
      "//unknown/components/brave_wallet/browser",
      "//unknown/components/brave_wallet/browser:pref_names",
    ]
  }
}
