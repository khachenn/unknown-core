# Copyright (c) 2025 The Brave Authors. All rights reserved.
# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this file,
# You can obtain one at https://mozilla.org/MPL/2.0/.

import("//unknown/components/ai_chat/core/common/buildflags/buildflags.gni")
import("//unknown/components/brave_vpn/common/buildflags/buildflags.gni")
import("//unknown/components/brave_wallet/common/buildflags/buildflags.gni")
import("//unknown/components/brave_wayback_machine/buildflags/buildflags.gni")
import("//unknown/components/ipfs/buildflags/buildflags.gni")
import("//unknown/components/speedreader/common/buildflags/buildflags.gni")
import("//unknown/components/tor/buildflags/buildflags.gni")
import("//build/config/features.gni")

source_set("brave_origin") {
  sources = [
    "brave_origin_service_factory.cc",
    "brave_origin_service_factory.h",
  ]

  deps = [
    "//base",
    "//unknown/browser/policy:brave_simple_policy_map",
    "//unknown/components/ai_chat/core/common/buildflags",
    "//unknown/components/brave_news/common",
    "//unknown/components/brave_origin",
    "//unknown/components/brave_policy",
    "//unknown/components/brave_rewards/core",
    "//unknown/components/brave_vpn/common/buildflags",
    "//unknown/components/brave_wallet/common/buildflags",
    "//unknown/components/brave_wayback_machine/buildflags",
    "//unknown/components/constants",
    "//unknown/components/ipfs/buildflags",
    "//unknown/components/p3a",
    "//unknown/components/speedreader/common/buildflags",
    "//unknown/components/tor/buildflags",
    "//chrome/browser:browser_process",
    "//chrome/browser/profiles",
    "//components/keyed_service/content",
    "//components/policy/core/browser",
    "//components/policy/core/common",
  ]

  if (is_android) {
    sources += [ "android/brave_origin_service_factory_android.cc" ]
    deps += [ "//unknown/browser/brave_origin/android:jni_headers" ]
  }

  if (enable_ai_chat) {
    deps += [ "//unknown/components/ai_chat/core/common" ]
  }

  if (enable_brave_vpn) {
    deps += [ "//unknown/components/brave_vpn/common" ]
  }

  if (enable_brave_wayback_machine) {
    deps += [ "//unknown/components/brave_wayback_machine" ]
  }

  if (enable_speedreader) {
    deps += [ "//unknown/components/speedreader" ]
  }

  if (enable_tor) {
    deps += [ "//unknown/components/tor:pref_names" ]
  }

  if (deprecate_ipfs) {
    deps += [ "//unknown/components/ipfs" ]
  }

  if (enable_brave_wallet) {
    deps += [ "//unknown/components/brave_wallet/browser:pref_names" ]
  }
}

source_set("unit_tests") {
  testonly = true

  sources = [ "brave_origin_service_factory_unittest.cc" ]

  deps = [
    ":brave_origin",
    "//base/test:test_support",
    "//unknown/browser/policy:brave_simple_policy_map",
    "//unknown/components/brave_origin",
    "//unknown/components/brave_rewards/core",
    "//unknown/components/constants",
    "//unknown/components/p3a",
    "//chrome/browser/profiles",
    "//chrome/test:test_support",
    "//components/policy/core/common:test_support",
    "//content/test:test_support",
    "//testing/gtest",
  ]

  if (enable_tor) {
    deps += [ "//unknown/components/tor:pref_names" ]
  }
}
